group 'com.claimvantage'
version '0.1-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

sourceSets {
  main {
    groovy {
      srcDirs = ['vars', 'src']
    }
  }

  test {
    groovy {
      srcDirs = ['test']
    }
  }
}

test {
    // delete old test reports
    dependsOn cleanTest

    // show full exception
    testLogging.exceptionFormat = 'full'

    // show stdout from tests
    onOutput { dest, event -> print event.message }

    // show test results
    def results = []
    afterTest { desc, result -> println "${desc.className.split("\\.")[-1]}: " + 
        "${desc.name}: ${result.resultType}" }
    afterSuite { desc, result -> if (desc.className) { results << result } }

    // show summary
    doLast {
        println ""
        println "TEST SUMMARY"
        println "Tests: ${results.sum { it.testCount }}" +
            ", Failures: ${results.sum { it.failedTestCount }}" +
            ", Errors: ${results.sum { it.exceptions.size() }}" + 
            ", Skipped: ${results.sum { it.skippedTestCount }}" 
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  maven { url 'https://repo.jenkins-ci.org/releases/' }
  maven { url 'https://repo.jenkins-ci.org/public/' }
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.5.11'

  // Jenkins plugins
  // compile 'org.jenkins-ci.plugins:credentials:2.1.13'
  // compile 'org.jenkins-ci.plugins:credentials-binding:1.13'
  // compile 'org.apache.maven:maven-model:3.3.9'

  testImplementation 'junit:junit:4.13'
  testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'

  /**
    * JenkinsPipelineUnit for testing pipelines from:
    * https://github.com/jenkinsci/JenkinsPipelineUnit
   */
  testImplementation "com.lesfurets:jenkins-pipeline-unit:1.3"

  // TRICKY: The lib folder contains all other plugins *JAR* files
  // if not found in Maven
  compile fileTree(dir: 'lib', include: ['*.jar'])

  implementation group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.7.1'
}
